apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    defaultConfig {
        versionCode project.hasProperty('versionCode') ? Integer.parseInt(project.versionCode) : Integer.parseInt(Calendar.getInstance().format("yyyyMMdd00"))
        versionName "1.0.0"
        multiDexEnabled true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${applicationId}-${variant.productFlavors[0].name}-v${variant.versionName}-${variant.versionCode}-${variant.productFlavors[1].name}-${variant.buildType.name}.apk"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "store", "apiEndpoint"

    productFlavors {
        google {
            dimension "store"
        }
        amazon {
            dimension "store"
        }
        prod {
            dimension "apiEndpoint"
        }
        dev {
            dimension "apiEndpoint"
        }
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}